plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

def currentVersionCode = 4
def currentVersionName = '1.0.3'
def siteUrl = 'https://github.com/fuzeman/MarkdownView'
def gitUrl = 'https://github.com/fuzeman/MarkdownView.git'

group = 'net.dgardiner.mdv'
version = currentVersionName

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'MarkdownView'
        userOrg = user
        licenses = ['Apache-2.0']

        vcsUrl = gitUrl
    }
}

ext {
    PUBLISH_GROUP_ID = 'net.dgardiner.mdv'
    PUBLISH_ARTIFACT_ID = 'markdownview'
    PUBLISH_VERSION = currentVersionName
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode currentVersionCode
        versionName currentVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name 'MarkdownView provides a simple method of displaying Markdown in Android applications'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'fuzeman'
                        name 'Dean Gardiner'
                        email 'me@dgardiner.net'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'net.dgardiner.markdown:markdown:1.0.0'

    testCompile 'junit:junit:4.12'
}

apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'